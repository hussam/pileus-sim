# Note you need gnuplot 4.4 for the pdfcairo terminal.

set terminal pdf font "Gill Sans,9" linewidth 4 rounded

# Line style for axes
set style line 80 lt rgb "#808080"

# Line style for grid
set style line 81 lt 0  # dashed
set style line 81 lt rgb "#808080"  # grey

set grid back linestyle 81
set border 3 back linestyle 80 # Remove border on top and right.  These

# borders are useless and make it harder
# to see plotted lines near the border.
# Also, put it in grey; no need for so much
# emphasis on a border.

set xtics nomirror
set ytics nomirror

#set log x
#set mxtics 10    # Makes logscale look good.

# Line styles: try to pick pleasing colors, rather
# than strictly primary colors or hard-to-see
# colors
# like gnuplot's default yellow.  Make the lines
# thick
# so they're easy to see in small plots in papers.
set style line 1 lt rgb "#A00000" lw 2 pt 1
set style line 2 lt rgb "#00A000" lw 2 pt 6
set style line 3 lt rgb "#5060D0" lw 2 pt 2
set style line 4 lt rgb "#F25900" lw 2 pt 9

set xlabel "Read %"

set key top right

set xrange [0:100]

set output "staleness_vs_read_pct.pdf"
set ylabel "Stale Reads %"
set yrange [0:110]
plot \
     "rr20.csv" using 3:6 title "20ms" w lp ls 1,\
     "rr25.csv" using 3:6 title "25ms" w lp ls 2,\
     "rr30.csv" using 3:6 title "30ms" w lp ls 3,\
     "rr40.csv" using 3:6 title "40ms" w lp ls 4,\
     "rr60.csv" using 3:6 title "60ms" w lp ls 5,\
     "rr80.csv" using 3:6 title "80ms" w lp ls 6

set output "inconsistency_vs_read_pct.pdf"
set ylabel "Inconsistent Reads %"
set yrange [0:110]
plot \
     "rr20.csv" using 3:8 title "20ms" w lp ls 1,\
     "rr25.csv" using 3:8 title "25ms" w lp ls 2,\
     "rr30.csv" using 3:8 title "30ms" w lp ls 3,\
     "rr40.csv" using 3:8 title "40ms" w lp ls 4,\
     "rr60.csv" using 3:8 title "60ms" w lp ls 5,\
     "rr80.csv" using 3:8 title "80ms" w lp ls 6

set output "freshness_vs_read_pct.pdf"
set ylabel "# lagging updates"
set yrange [0:1000]
plot \
     "rr20.csv" using 3:10:11 title "20ms" w errorlines ls 1,\
     "rr25.csv" using 3:10:11 title "25ms" w errorlines ls 2,\
     "rr30.csv" using 3:10:11 title "30ms" w errorlines ls 3,\
     "rr40.csv" using 3:10:11 title "40ms" w errorlines ls 4,\
     "rr60.csv" using 3:10:11 title "60ms" w errorlines ls 5,\
     "rr80.csv" using 3:10:11 title "80ms" w errorlines ls 6
